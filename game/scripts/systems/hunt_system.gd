extends Node
## 狩獵場：單人波次、每日有獎 cap。後期可接多人房。
## Autoload：HuntSystem

const ContentLoc := preload("res://scripts/systems/content_loc.gd")

const DAILY_CAP := 5
## 練習場次（日 cap 用完後）的獎勵倍率：金、材料、經驗共用一個數字
const PRACTICE_MULT := 0.35

## 三波敵人（WorldContent mode）
const WAVES: Array[Dictionary] = [
	{"mode": "ash_rat", "label": "第一波 · 灰燼鼠潮"},
	{"mode": "road_bandit", "label": "第二波 · 溢地殘兵"},
	{"mode": "scar_wisp", "label": "第三波 · 焰靈"},
]



## 玩家看得到的中文字面值一律包這支（以原文當 key，譯文在
## data/i18n/content/<locale>/ui.json）。務必包在格式化之前 ——
## `_t("A %d") % [n]` 查得到表，`_t("A %d" % [n])` 查不到。
static func _t(s: String) -> String:
	return ContentLoc.text("ui", s)

func _ready() -> void:
	pass


func _fk(suffix: String) -> String:
	return "hunt.%s" % suffix


func today_key() -> String:
	var d: Dictionary = Time.get_date_dict_from_system()
	return "%04d-%02d-%02d" % [int(d.year), int(d.month), int(d.day)]


func _refresh_daily() -> void:
	var today := today_key()
	if str(GameState.get_flag(_fk("day"), "")) != today:
		GameState.set_flag(_fk("day"), today)
		GameState.set_flag(_fk("runs_today"), 0)


func runs_today() -> int:
	_refresh_daily()
	return int(GameState.get_flag(_fk("runs_today"), 0))


func daily_left() -> int:
	return maxi(0, DAILY_CAP - runs_today())


func is_unlocked() -> bool:
	return GameState.has_flag("c1_entered_city") or GameState.chapter != "c0" \
		or GameState.has_flag("c0_first_battle")


func is_run_active() -> bool:
	return bool(GameState.get_flag(_fk("active"), false))


func current_wave() -> int:
	return int(GameState.get_flag(_fk("wave"), 0))


func is_practice() -> bool:
	return bool(GameState.get_flag(_fk("practice"), false))


func status_bbcode() -> String:
	_refresh_daily()
	var lines: PackedStringArray = []
	lines.append(_t("[b]野外獵場[/b]"))
	lines.append(_t("黑焰溢地的邊陲。拿材料的地方，經驗比演武場薄。"))
	lines.append("")
	if not is_unlocked():
		lines.append(_t("（進入騎士堡後解鎖）"))
		return "\n".join(lines)
	lines.append(_t("今日有獎場次：%d／%d（剩餘 %d）") % [runs_today(), DAILY_CAP, daily_left()])
	lines.append(_t("每場：3 波雜魚。每波薄經驗，通關再發材料與金。要練等回村演武。"))
	lines.append(_t("有獎場次用完後仍可練習，但經驗與金都只剩三成五。"))
	if is_run_active():
		lines.append(_t("[color=#c96]進行中：第 %d／%d 波[/color]") % [current_wave() + 1, WAVES.size()])
	lines.append("")
	lines.append(_t("掉落：溢皮、焰骨、溢核（可在溢物回收換金）"))
	return "\n".join(lines)


## 開始一場（有獎或練習）
func start_run(force_practice: bool = false) -> Dictionary:
	if not is_unlocked():
		return {"ok": false, "msg": _t("尚未解鎖狩獵場。")}
	if is_run_active():
		return {"ok": false, "msg": _t("已有進行中的狩獵。請先打完或放棄。")}
	_refresh_daily()
	var practice := force_practice or daily_left() <= 0
	GameState.set_flag(_fk("active"), true)
	GameState.set_flag(_fk("wave"), 0)
	GameState.set_flag(_fk("practice"), practice)
	GameState.set_flag(_fk("waves_cleared"), 0)
	SaveManager.save_game()
	var msg := _t("狩獵開始（練習·獎勵減少）。") if practice else _t("狩獵開始（有獎）。")
	return {
		"ok": true,
		"practice": practice,
		"mode": str(WAVES[0].get("mode", "ash_rat")),
		"label": _t(str(WAVES[0].get("label", "第一波"))),
		"msg": msg,
	}


func abandon_run() -> void:
	GameState.set_flag(_fk("active"), false)
	GameState.set_flag(_fk("wave"), 0)
	GameState.set_flag(_fk("practice"), false)
	SaveManager.save_game()


func wave_label(index: int = -1) -> String:
	var i := current_wave() if index < 0 else index
	if i < 0 or i >= WAVES.size():
		return ""
	return _t(str(WAVES[i].get("label", "波次")))


func wave_mode(index: int = -1) -> String:
	var i := current_wave() if index < 0 else index
	if i < 0 or i >= WAVES.size():
		return "ash_rat"
	return str(WAVES[i].get("mode", "ash_rat"))


## 勝利一波：回傳 {ok, finished, next_mode?, next_label?, loot_msg, msg}
## 打贏一波要給的經驗＝野外同一隻（薄）。材料才是獵場多出來的。
func wave_xp(mode: String, practice: bool) -> int:
	var def: Dictionary = WorldContent.enemy_def(mode)
	var xp_n := Formulas.field_xp(int(def.get("max_hp", 50)), 0)
	if practice:
		xp_n = maxi(1, int(round(float(xp_n) * PRACTICE_MULT)))
	return maxi(1, xp_n)


func on_wave_won() -> Dictionary:
	if not is_run_active():
		return {"ok": false, "msg": _t("沒有進行中的狩獵。")}
	var w := current_wave()
	var practice := is_practice()
	var cleared := int(GameState.get_flag(_fk("waves_cleared"), 0)) + 1
	GameState.set_flag(_fk("waves_cleared"), cleared)
	## 這一波的經驗（先結，通關那波也算）
	var xr: Dictionary = GameState.add_xp(wave_xp(wave_mode(w), practice))
	var xp_got := int(xr.get("gained", 0))
	var lv_up := int(xr.get("levels", 0)) > 0
	## 波次小掉落
	var mid_loot := _roll_wave_loot(false)
	if w + 1 >= WAVES.size():
		## 通關整場
		var fin := _finish_run(true)
		fin["wave_loot"] = mid_loot
		fin["xp"] = xp_got
		fin["level_up"] = lv_up
		return fin
	GameState.set_flag(_fk("wave"), w + 1)
	SaveManager.save_game()
	var nw := w + 1
	return {
		"ok": true,
		"finished": false,
		"next_mode": wave_mode(nw),
		"next_label": wave_label(nw),
		"loot_msg": mid_loot,
		"xp": xp_got,
		"level_up": lv_up,
		"msg": _t("擊破！%s") % wave_label(nw),
	}


func on_wave_lost() -> Dictionary:
	if not is_run_active():
		return {"ok": false, "msg": ""}
	var cleared := int(GameState.get_flag(_fk("waves_cleared"), 0))
	var pity := ""
	if cleared > 0 and not is_practice():
		pity = _grant_items({"hunt_hide": 1})
	abandon_run()
	var msg := _t("狩獵中斷。")
	if pity != "":
		msg += _t(" 保底：%s") % pity
	return {"ok": true, "msg": msg, "loot_msg": pity}


func _finish_run(full_clear: bool) -> Dictionary:
	var practice := is_practice()
	if full_clear:
		## 原作「一鍵戰鬥」：首次手動全通後開放
		GameState.set_flag("meta.hunt_auto_unlocked", true)
	if full_clear and not practice:
		_refresh_daily()
		GameState.set_flag(_fk("runs_today"), runs_today() + 1)
		GameState.set_flag(_fk("clears_total"), int(GameState.get_flag(_fk("clears_total"), 0)) + 1)
		## 每日委託「星途一狩」
		QuestSystem.track_day("hunt", 1)
	## 經濟 0.15：狩獵場金幣略降（舊 40+12×波 → 30+10×波），練習局仍 ×PRACTICE_MULT。
	## 可重複日刷，不可壓過「鍛造／職系武器」主 sink。
	var gold_n := 30 + int(GameState.get_flag(_fk("waves_cleared"), 0)) * 10
	if practice:
		gold_n = int(gold_n * PRACTICE_MULT)
	GameState.add_gold(gold_n)
	var loot_msg := _roll_wave_loot(true)
	if not practice and full_clear:
		## 通關加碼
		var extra := _grant_items({"hunt_bone": 1})
		if randf() < 0.35:
			extra += _grant_items({"hunt_core": 1})
		if randf() < 0.4:
			InventorySystem.add_item("hp_s", 1)
			extra += _t(" 小紅水×1")
		## 寶石碎片（手藝工坊熔煉原料）
		if Engine.get_main_loop() is SceneTree:
			var gem: Node = (Engine.get_main_loop() as SceneTree).root.get_node_or_null("GemSystem")
			if gem != null and gem.has_method("add_shards") and randf() < 0.65:
				var cols: Array = ["red", "yellow", "blue"]
				var col: String = str(cols[randi() % 3])
				var qty := 2 if randf() < 0.45 else 1
				gem.call("add_shards", col, qty)
				extra += _t(" 寶石碎片×%d（%s）") % [qty, str(gem.call("color_label", col))]
		if Engine.get_main_loop() is SceneTree:
			var inv2: Node = (Engine.get_main_loop() as SceneTree).root.get_node_or_null("InventorySystem")
			if inv2 != null and inv2.has_method("add_item") and randf() < 0.55:
				inv2.call("add_item", "medal", 1)
				extra += _t(" 勳章×1")
		loot_msg = (loot_msg + " " + extra).strip_edges()
	elif practice and full_clear:
		loot_msg = _grant_items({"hunt_hide": 1})
	GameState.set_flag(_fk("active"), false)
	GameState.set_flag(_fk("wave"), 0)
	GameState.set_flag(_fk("practice"), false)
	SaveManager.save_game()
	return {
		"ok": true,
		"finished": true,
		"gold": gold_n,
		"loot_msg": loot_msg,
		"msg": _t("狩獵完成！金 +%d。%s") % [gold_n, loot_msg],
		"practice": practice,
	}


func _roll_wave_loot(finale: bool) -> String:
	var bag: Dictionary = {}
	if randf() < (0.7 if finale else 0.45):
		bag["hunt_hide"] = 1 + (1 if finale and randf() < 0.3 else 0)
	if finale and randf() < 0.55:
		bag["hunt_bone"] = 1
	if finale and randf() < 0.2:
		bag["hunt_core"] = 1
	if is_practice():
		## 練習大砍
		if bag.has("hunt_core"):
			bag.erase("hunt_core")
		if bag.has("hunt_bone") and randf() < 0.5:
			bag.erase("hunt_bone")
	return _grant_items(bag)


func _grant_items(bag: Dictionary) -> String:
	var parts: PackedStringArray = []
	for id in bag.keys():
		var n := int(bag[id])
		if n <= 0:
			continue
		if InventorySystem.add_item(str(id), n):
			parts.append("%s×%d" % [InventorySystem.item_name(str(id)), n])
	if parts.is_empty():
		return ""
	return _t("獲得 ") + "、".join(parts)


## NPC 回收價（假市集）
func recycle_price(item_id: String) -> int:
	match item_id:
		"hunt_hide":
			return 10
		"hunt_bone":
			return 22
		"hunt_core":
			return 60
		_:
			var def: Dictionary = InventorySystem.catalog(item_id)
			return int(def.get("sell", 0))


func recycle_one(item_id: String) -> Dictionary:
	var price := recycle_price(item_id)
	if price <= 0:
		return {"ok": false, "msg": _t("此物不收。")}
	if not InventorySystem.has_item(item_id, 1):
		return {"ok": false, "msg": _t("沒有此物。")}
	InventorySystem.remove_item(item_id, 1)
	GameState.add_gold(price)
	## 每日委託「材料回收：賣出材料累計 5 件」—— 溢物回收也是賣材料，要算進去。
	## 原本只有琥珀的一鍵賣出會算，獵場刷完回收一輪，委託還是 0／5。
	QuestSystem.track_day("sell", 1)
	SaveManager.save_game()
	return {"ok": true, "msg": _t("回收【%s】· 金 +%d") % [InventorySystem.item_name(item_id), price]}
